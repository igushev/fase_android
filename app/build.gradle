apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.fase"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"

        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "${variant.applicationId}-ver-${variant.versionName}-verCode${variant.versionCode}-${variant.buildType.name}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
            storePassword "faseapp2018"
            keyAlias "faseappdebug"
            keyPassword "faseapp2018"
        }

        release {
            storeFile file("../keystore/release.jks")
            storePassword "faseapp2018"
            keyAlias "faseapprelease"
            keyPassword "faseapp2018"
        }
    }

    buildTypes {
        all {
            buildConfigField 'Integer', 'DB_VERSION', '1'
            buildConfigField 'String', 'DB_NAME', '"faseDb"'
            buildConfigField 'Integer', 'CONNECTION_TIMEOUT', '30'
            buildConfigField 'String', 'SHARED_PREFS_NAME', '"faseSharedPrefs"'
            buildConfigField 'String', 'SECURE_SHARED_PREFS_KEY', '"59f1060c-8fa1-4967-9566-98e713aaa429"'
            buildConfigField 'String', 'DEVICE_TYPE', '"Android"'
            buildConfigField 'Integer', 'ELEMENT_UPDATE_TIME', '2'

            resValue 'string', 'google_maps_api_key', '""'
        }

        debug {
            buildConfigField 'String', 'API_HOST', '"http://hello-world-fase-env-prod.us-west-2.elasticbeanstalk.com/"'

            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField 'String', 'API_HOST', '"http://hello-world-fase-env-prod.us-west-2.elasticbeanstalk.com/"'

            debuggable false
            minifyEnabled false
            useProguard false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            resConfigs "en", "xxhdpi"
        }

        prod {
            dimension "app"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

ext {
    appCompatLib = "1.1.0-beta01"
    materialLib = "1.1.0-alpha07"
    timber = "4.7.1"
    joda = "2.10.1.2"
    moxyX = "1.7.0"
    butterknife = "10.1.0"
    rxJava = "2.2.10"
    rxAndroid = "2.1.1"
    rxBinding = "3.0.0-alpha2"
    rxPermisiions = "0.9.5"
    retrofitLibs = "2.6.0"
    okhttpLibs = "3.14.2"
    glide = "4.9.0"
    rxJavaDebug = "1.4.0"
    playServices = "17.0.0"
    firebaseMessaging = "19.0.1"
    firebaseCore = "17.0.0"
    crashlyticsVer = "2.10.1"
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatLib"
    implementation "com.google.android.material:material:$materialLib"

    implementation "com.jakewharton.timber:timber:$timber"
    implementation "net.danlew:android.joda:$joda"

    implementation "com.jakewharton:butterknife:$butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife"

    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBinding"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rxPermisiions@aar"
    implementation "com.akaita.java:rxjava2-debug:$rxJavaDebug"

    implementation "tech.schoolhelper:moxy-x-androidx:$moxyX"
    annotationProcessor "tech.schoolhelper:moxy-x-compiler:$moxyX"

    implementation "com.squareup.retrofit2:retrofit:$retrofitLibs"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibs"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibs"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibs"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibs"

    implementation "com.github.bumptech.glide:glide:$glide", {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide"

    implementation "com.google.android.gms:play-services-places:$playServices"
    implementation "com.google.firebase:firebase-messaging:$firebaseMessaging"
    implementation "com.google.firebase:firebase-core:$firebaseCore"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVer"
}

apply plugin: 'com.google.gms.google-services'
