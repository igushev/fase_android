apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.fase"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.0.1"

        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "${variant.applicationId}-ver-${variant.versionName}-verCode${variant.versionCode}-${variant.buildType.name}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
            storePassword "faseapp2018"
            keyAlias "faseappdebug"
            keyPassword "faseapp2018"
        }

        release {
            storeFile file("../keystore/debug.keystore")
            storePassword "faseapp2018"
            keyAlias "faseappdebug"
            keyPassword "faseapp2018"
//            storeFile file("../keystore/release.jks")
//            storePassword "pass"
//            keyAlias "fase"
//            keyPassword "pass"
        }
    }

    buildTypes {
        all {
            buildConfigField 'Integer', 'CONNECTION_TIMEOUT', '30'
            buildConfigField 'String', 'SHARED_PREFS_NAME', '"faseSharedPrefs"'
            buildConfigField 'String', 'SECURE_SHARED_PREFS_KEY', '"59f1060c-8fa1-4967-9566-98e713aaa429"'
            buildConfigField 'String', 'API_HOST', '"http://fasehelloworld-env.us-west-2.elasticbeanstalk.com"'

            buildConfigField 'String', 'DEVICE_TYPE', '"Android"'
        }

        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            minifyEnabled true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            resConfigs "en", "xxhdpi"
        }

        prod {
            dimension "app"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

ext {
    supportLib = "27.1.0"
    timber = "4.6.1"
    joda = "2.9.9.1"
    moxy = "1.5.3"
    butterknife = "8.8.1"
    rxJava = "2.1.7"
    rxAndroid = "2.0.2"
    rxBinding = "2.1.1"
    rxPermisiions = "0.9.5"
    retrofitLibs = "2.3.0"
    okhttpLibs = "3.9.1"
    jobDispatcher = "0.8.5"
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLib"
    implementation "com.android.support:design:$supportLib"

    implementation "com.jakewharton.timber:timber:$timber"
    implementation "net.danlew:android.joda:$joda"

    implementation "com.jakewharton:butterknife:$butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife"

    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxBinding"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rxPermisiions@aar"

    implementation "com.arello-mobile:moxy:$moxy"
    implementation "com.arello-mobile:moxy-app-compat:$moxy"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxy"

    implementation "com.squareup.retrofit2:retrofit:$retrofitLibs"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibs"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibs"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibs"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibs"

    implementation "com.firebase:firebase-jobdispatcher:$jobDispatcher"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportLib"
            }
        }
    }
}
